plugins {
    //使用 Kotlin 语言开发必备
    id 'org.jetbrains.kotlin.jvm'
    //用于帮助开发 Gradle 插件，会自动应用 Java Library 插件，并在 dependencies 中添加 implementation gradleApi()
    id 'java-gradle-plugin'
    //用于发布至maven仓库的插件
    id 'maven-publish'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

//配置插件实现类，自动生成插件properties（需要build一下），用于gradle发现插件入口【配置生成位置：build/resources/main/META-INF/gradle-plugins，并在打包jar时被添加到最终jar包中】
gradlePlugin {
    plugins {
        modularPlugin {
            id = 'privacyPlugin'
            implementationClass = 'com.eric.gradle.plugin.privacy.PrivacyPlugin'
        }
    }
}

//插件发布(gradle 7.x后使用下面的方式发布插件)
group = 'com.eric.gradle.plugin.privacy'
version = "$sdk_version"
publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'privacyPlugin'
        }
    }

    repositories {
        maven {
            url = uri('../maven-repo')
        }
    }
}

dependencies {
    //java-gradle-plugin已默认引入
    //implementation gradleApi()
    //asm核心库
    implementation 'org.ow2.asm:asm:9.3'
    //asm辅助库
    implementation 'org.ow2.asm:asm-commons:9.3'
    implementation 'org.ow2.asm:asm-analysis:9.3'
    implementation 'org.ow2.asm:asm-util:9.3'
    implementation 'org.ow2.asm:asm-tree:9.3'

    //apache IO库
    implementation 'commons-io:commons-io:2.11.0'
    //gson
    implementation 'com.google.code.gson:gson:2.9.0'

    implementation project(':privacyAnnotation')
    // AGP 7.2.1
    implementation("com.android.tools.build:gradle:7.2.1") {
        //防止与其他第三方库中的asm版本冲突
        exclude(group: 'org.ow2.asm')
    }
}